<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Type.BetaNBE</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Markup">\begin{code}</a>
<a id="14" class="Keyword">module</a> <a id="21" href="Type.BetaNBE.html" class="Module">Type.BetaNBE</a> <a id="34" class="Keyword">where</a>
<a id="40" class="Markup">\end{code}</a><a id="50" class="Background">

## Imports

</a><a id="64" class="Markup">\begin{code}</a>
<a id="77" class="Keyword">open</a> <a id="82" class="Keyword">import</a> <a id="89" href="Type.html" class="Module">Type</a>
<a id="94" class="Keyword">open</a> <a id="99" class="Keyword">import</a> <a id="106" href="Type.BetaNormal.html" class="Module">Type.BetaNormal</a>
<a id="122" class="Keyword">open</a> <a id="127" class="Keyword">import</a> <a id="134" href="Type.RenamingSubstitution.html" class="Module">Type.RenamingSubstitution</a>
<a id="160" class="Keyword">open</a> <a id="165" class="Keyword">import</a> <a id="172" href="Type.Equality.html" class="Module">Type.Equality</a>

<a id="187" class="Keyword">open</a> <a id="192" class="Keyword">import</a> <a id="199" href="Function.html" class="Module">Function</a>
<a id="208" class="Keyword">open</a> <a id="213" class="Keyword">import</a> <a id="220" href="Data.Sum.html" class="Module">Data.Sum</a>
<a id="229" class="Keyword">open</a> <a id="234" class="Keyword">import</a> <a id="241" href="Data.Empty.html" class="Module">Data.Empty</a>
<a id="252" class="Keyword">open</a> <a id="257" class="Keyword">import</a> <a id="264" href="Data.Product.html" class="Module">Data.Product</a>
<a id="277" class="Keyword">open</a> <a id="282" class="Keyword">import</a> <a id="289" href="Data.String.html" class="Module">Data.String</a>

<a id="302" class="Keyword">open</a> <a id="307" class="Keyword">import</a> <a id="314" href="Relation.Binary.PropositionalEquality.html" class="Module">Relation.Binary.PropositionalEquality</a> <a id="352" class="Keyword">hiding</a> <a id="359" class="Symbol">(</a><a id="360" href="Relation.Binary.PropositionalEquality.html#3347" class="InductiveConstructor Operator">[_]</a><a id="363" class="Symbol">;</a> <a id="365" href="Relation.Binary.PropositionalEquality.Core.html#978" class="Function">subst</a><a id="370" class="Symbol">)</a>
<a id="372" class="Markup">\end{code}</a><a id="382" class="Background">

Values are defined by induction on kind. At kind # and * they are
inert and defined to be just normal forms. At function kind they are
either neutral or Kripke functions

</a><a id="555" class="Markup">\begin{code}</a>
<a id="Val"></a><a id="568" href="Type.BetaNBE.html#568" class="Function">Val</a> <a id="572" class="Symbol">:</a> <a id="574" href="Type.html#866" class="Datatype">Ctx⋆</a> <a id="579" class="Symbol">→</a> <a id="581" href="Type.html#596" class="Datatype">Kind</a> <a id="586" class="Symbol">→</a> <a id="588" class="PrimitiveType">Set</a>
<a id="592" href="Type.BetaNBE.html#568" class="Function">Val</a> <a id="596" href="Type.BetaNBE.html#596" class="Bound">Φ</a> <a id="598" href="Type.html#615" class="InductiveConstructor">*</a>       <a id="606" class="Symbol">=</a> <a id="608" href="Type.BetaNBE.html#596" class="Bound">Φ</a> <a id="610" href="Type.BetaNormal.html#885" class="Datatype Operator">⊢Nf⋆</a> <a id="615" href="Type.html#615" class="InductiveConstructor">*</a>
<a id="617" href="Type.BetaNBE.html#568" class="Function">Val</a> <a id="621" href="Type.BetaNBE.html#621" class="Bound">Φ</a> <a id="623" class="Symbol">(</a><a id="624" href="Type.BetaNBE.html#624" class="Bound">σ</a> <a id="626" href="Type.html#650" class="InductiveConstructor Operator">⇒</a> <a id="628" href="Type.BetaNBE.html#628" class="Bound">τ</a><a id="629" class="Symbol">)</a> <a id="631" class="Symbol">=</a> <a id="633" href="Type.BetaNBE.html#621" class="Bound">Φ</a> <a id="635" href="Type.BetaNormal.html#918" class="Datatype Operator">⊢Ne⋆</a> <a id="640" class="Symbol">(</a><a id="641" href="Type.BetaNBE.html#624" class="Bound">σ</a> <a id="643" href="Type.html#650" class="InductiveConstructor Operator">⇒</a> <a id="645" href="Type.BetaNBE.html#628" class="Bound">τ</a><a id="646" class="Symbol">)</a> <a id="648" href="Data.Sum.Base.html#509" class="Datatype Operator">⊎</a> <a id="650" href="Agda.Builtin.String.html#206" class="Postulate">String</a> <a id="657" href="Data.Product.html#1389" class="Function Operator">×</a> <a id="659" class="Symbol">∀</a> <a id="661" class="Symbol">{</a><a id="662" href="Type.BetaNBE.html#662" class="Bound">Ψ</a><a id="663" class="Symbol">}</a> <a id="665" class="Symbol">→</a> <a id="667" href="Type.RenamingSubstitution.html#378" class="Function">Ren</a> <a id="671" href="Type.BetaNBE.html#621" class="Bound">Φ</a> <a id="673" href="Type.BetaNBE.html#662" class="Bound">Ψ</a> <a id="675" class="Symbol">→</a> <a id="677" href="Type.BetaNBE.html#568" class="Function">Val</a> <a id="681" href="Type.BetaNBE.html#662" class="Bound">Ψ</a> <a id="683" href="Type.BetaNBE.html#624" class="Bound">σ</a> <a id="685" class="Symbol">→</a> <a id="687" href="Type.BetaNBE.html#568" class="Function">Val</a> <a id="691" href="Type.BetaNBE.html#662" class="Bound">Ψ</a> <a id="693" href="Type.BetaNBE.html#628" class="Bound">τ</a>
<a id="695" class="Markup">\end{code}</a><a id="705" class="Background">

We can embed neutral terms into values at any kind using reflect.
reflect is quite simple in this version of NBE and not mutually
defined with reify.

</a><a id="858" class="Markup">\begin{code}</a>
<a id="reflect"></a><a id="871" href="Type.BetaNBE.html#871" class="Function">reflect</a> <a id="879" class="Symbol">:</a> <a id="881" class="Symbol">∀{</a><a id="883" href="Type.BetaNBE.html#883" class="Bound">Φ</a> <a id="885" href="Type.BetaNBE.html#885" class="Bound">σ</a><a id="886" class="Symbol">}</a> <a id="888" class="Symbol">→</a> <a id="890" href="Type.BetaNBE.html#883" class="Bound">Φ</a> <a id="892" href="Type.BetaNormal.html#918" class="Datatype Operator">⊢Ne⋆</a> <a id="897" href="Type.BetaNBE.html#885" class="Bound">σ</a> <a id="899" class="Symbol">→</a> <a id="901" href="Type.BetaNBE.html#568" class="Function">Val</a> <a id="905" href="Type.BetaNBE.html#883" class="Bound">Φ</a> <a id="907" href="Type.BetaNBE.html#885" class="Bound">σ</a>
<a id="909" href="Type.BetaNBE.html#871" class="Function">reflect</a> <a id="917" class="Symbol">{</a><a id="918" class="Argument">σ</a> <a id="920" class="Symbol">=</a> <a id="922" href="Type.html#615" class="InductiveConstructor">*</a><a id="923" class="Symbol">}</a>     <a id="929" href="Type.BetaNBE.html#929" class="Bound">n</a> <a id="931" class="Symbol">=</a> <a id="933" href="Type.BetaNormal.html#1449" class="InductiveConstructor">ne</a> <a id="936" href="Type.BetaNBE.html#929" class="Bound">n</a>
<a id="938" href="Type.BetaNBE.html#871" class="Function">reflect</a> <a id="946" class="Symbol">{</a><a id="947" class="Argument">σ</a> <a id="949" class="Symbol">=</a> <a id="951" href="Type.BetaNBE.html#951" class="Bound">σ</a> <a id="953" href="Type.html#650" class="InductiveConstructor Operator">⇒</a> <a id="955" href="Type.BetaNBE.html#955" class="Bound">τ</a><a id="956" class="Symbol">}</a> <a id="958" href="Type.BetaNBE.html#958" class="Bound">n</a> <a id="960" class="Symbol">=</a> <a id="962" href="Data.Sum.Base.html#565" class="InductiveConstructor">inj₁</a> <a id="967" href="Type.BetaNBE.html#958" class="Bound">n</a>
<a id="969" class="Markup">\end{code}</a><a id="979" class="Background">

A shorthand for creating a new fresh variable as a value which we need
in reify

</a><a id="1062" class="Markup">\begin{code}</a>
<a id="fresh"></a><a id="1075" href="Type.BetaNBE.html#1075" class="Function">fresh</a> <a id="1081" class="Symbol">:</a> <a id="1083" class="Symbol">∀</a> <a id="1085" class="Symbol">{</a><a id="1086" href="Type.BetaNBE.html#1086" class="Bound">Φ</a> <a id="1088" href="Type.BetaNBE.html#1088" class="Bound">σ</a><a id="1089" class="Symbol">}</a> <a id="1091" class="Symbol">→</a> <a id="1093" href="Type.BetaNBE.html#568" class="Function">Val</a> <a id="1097" class="Symbol">(</a><a id="1098" href="Type.BetaNBE.html#1086" class="Bound">Φ</a> <a id="1100" href="Type.html#896" class="InductiveConstructor Operator">,⋆</a> <a id="1103" href="Type.BetaNBE.html#1088" class="Bound">σ</a><a id="1104" class="Symbol">)</a> <a id="1106" href="Type.BetaNBE.html#1088" class="Bound">σ</a>
<a id="1108" href="Type.BetaNBE.html#1075" class="Function">fresh</a> <a id="1114" class="Symbol">=</a> <a id="1116" href="Type.BetaNBE.html#871" class="Function">reflect</a> <a id="1124" class="Symbol">(</a><a id="1125" href="Type.BetaNormal.html#953" class="InductiveConstructor">`</a> <a id="1127" href="Type.html#1251" class="InductiveConstructor">Z</a><a id="1128" class="Symbol">)</a>
<a id="1130" class="Markup">\end{code}</a><a id="1140" class="Background">

Renaming for values

</a><a id="1163" class="Markup">\begin{code}</a>
<a id="renVal"></a><a id="1176" href="Type.BetaNBE.html#1176" class="Function">renVal</a> <a id="1183" class="Symbol">:</a> <a id="1185" class="Symbol">∀</a> <a id="1187" class="Symbol">{</a><a id="1188" href="Type.BetaNBE.html#1188" class="Bound">σ</a> <a id="1190" href="Type.BetaNBE.html#1190" class="Bound">Φ</a> <a id="1192" href="Type.BetaNBE.html#1192" class="Bound">Ψ</a><a id="1193" class="Symbol">}</a> <a id="1195" class="Symbol">→</a> <a id="1197" href="Type.RenamingSubstitution.html#378" class="Function">Ren</a> <a id="1201" href="Type.BetaNBE.html#1190" class="Bound">Φ</a> <a id="1203" href="Type.BetaNBE.html#1192" class="Bound">Ψ</a> <a id="1205" class="Symbol">→</a> <a id="1207" href="Type.BetaNBE.html#568" class="Function">Val</a> <a id="1211" href="Type.BetaNBE.html#1190" class="Bound">Φ</a> <a id="1213" href="Type.BetaNBE.html#1188" class="Bound">σ</a> <a id="1215" class="Symbol">→</a> <a id="1217" href="Type.BetaNBE.html#568" class="Function">Val</a> <a id="1221" href="Type.BetaNBE.html#1192" class="Bound">Ψ</a> <a id="1223" href="Type.BetaNBE.html#1188" class="Bound">σ</a>
<a id="1225" href="Type.BetaNBE.html#1176" class="Function">renVal</a> <a id="1232" class="Symbol">{</a><a id="1233" href="Type.html#615" class="InductiveConstructor">*</a><a id="1234" class="Symbol">}</a>     <a id="1240" href="Type.BetaNBE.html#1240" class="Bound">ψ</a> <a id="1242" href="Type.BetaNBE.html#1242" class="Bound">n</a>        <a id="1251" class="Symbol">=</a> <a id="1253" href="Type.BetaNormal.html#1715" class="Function">renNf</a> <a id="1259" href="Type.BetaNBE.html#1240" class="Bound">ψ</a> <a id="1261" href="Type.BetaNBE.html#1242" class="Bound">n</a>
<a id="1263" href="Type.BetaNBE.html#1176" class="Function">renVal</a> <a id="1270" class="Symbol">{</a><a id="1271" href="Type.BetaNBE.html#1271" class="Bound">σ</a> <a id="1273" href="Type.html#650" class="InductiveConstructor Operator">⇒</a> <a id="1275" href="Type.BetaNBE.html#1275" class="Bound">τ</a><a id="1276" class="Symbol">}</a> <a id="1278" href="Type.BetaNBE.html#1278" class="Bound">ψ</a> <a id="1280" class="Symbol">(</a><a id="1281" href="Data.Sum.Base.html#565" class="InductiveConstructor">inj₁</a> <a id="1286" href="Type.BetaNBE.html#1286" class="Bound">n</a><a id="1287" class="Symbol">)</a> <a id="1289" class="Symbol">=</a> <a id="1291" href="Data.Sum.Base.html#565" class="InductiveConstructor">inj₁</a> <a id="1296" class="Symbol">(</a><a id="1297" href="Type.BetaNormal.html#1811" class="Function">renNe</a> <a id="1303" href="Type.BetaNBE.html#1278" class="Bound">ψ</a> <a id="1305" href="Type.BetaNBE.html#1286" class="Bound">n</a><a id="1306" class="Symbol">)</a>
<a id="1308" href="Type.BetaNBE.html#1176" class="Function">renVal</a> <a id="1315" class="Symbol">{</a><a id="1316" href="Type.BetaNBE.html#1316" class="Bound">σ</a> <a id="1318" href="Type.html#650" class="InductiveConstructor Operator">⇒</a> <a id="1320" href="Type.BetaNBE.html#1320" class="Bound">τ</a><a id="1321" class="Symbol">}</a> <a id="1323" href="Type.BetaNBE.html#1323" class="Bound">ψ</a> <a id="1325" class="Symbol">(</a><a id="1326" href="Data.Sum.Base.html#590" class="InductiveConstructor">inj₂</a> <a id="1331" class="Symbol">(</a><a id="1332" href="Type.BetaNBE.html#1332" class="Bound">x</a> <a id="1334" href="Agda.Builtin.Sigma.html#209" class="InductiveConstructor Operator">,</a> <a id="1336" href="Type.BetaNBE.html#1336" class="Bound">f</a><a id="1337" class="Symbol">))</a> <a id="1340" class="Symbol">=</a> <a id="1342" href="Data.Sum.Base.html#590" class="InductiveConstructor">inj₂</a> <a id="1347" class="Symbol">(</a><a id="1348" href="Type.BetaNBE.html#1332" class="Bound">x</a> <a id="1350" href="Agda.Builtin.Sigma.html#209" class="InductiveConstructor Operator">,</a> <a id="1352" class="Symbol">λ</a> <a id="1354" href="Type.BetaNBE.html#1354" class="Bound">ρ&#39;</a> <a id="1357" class="Symbol">→</a>  <a id="1360" href="Type.BetaNBE.html#1336" class="Bound">f</a> <a id="1362" class="Symbol">(</a><a id="1363" href="Type.BetaNBE.html#1354" class="Bound">ρ&#39;</a> <a id="1366" href="Function.html#805" class="Function Operator">∘</a> <a id="1368" href="Type.BetaNBE.html#1323" class="Bound">ψ</a><a id="1369" class="Symbol">))</a>
<a id="1372" class="Markup">\end{code}</a><a id="1382" class="Background">

Weakening for values

</a><a id="1406" class="Markup">\begin{code}</a>
<a id="weakenVal"></a><a id="1419" href="Type.BetaNBE.html#1419" class="Function">weakenVal</a> <a id="1429" class="Symbol">:</a> <a id="1431" class="Symbol">∀</a> <a id="1433" class="Symbol">{</a><a id="1434" href="Type.BetaNBE.html#1434" class="Bound">σ</a> <a id="1436" href="Type.BetaNBE.html#1436" class="Bound">Φ</a> <a id="1438" href="Type.BetaNBE.html#1438" class="Bound">K</a><a id="1439" class="Symbol">}</a> <a id="1441" class="Symbol">→</a> <a id="1443" href="Type.BetaNBE.html#568" class="Function">Val</a> <a id="1447" href="Type.BetaNBE.html#1436" class="Bound">Φ</a> <a id="1449" href="Type.BetaNBE.html#1434" class="Bound">σ</a> <a id="1451" class="Symbol">→</a> <a id="1453" href="Type.BetaNBE.html#568" class="Function">Val</a> <a id="1457" class="Symbol">(</a><a id="1458" href="Type.BetaNBE.html#1436" class="Bound">Φ</a> <a id="1460" href="Type.html#896" class="InductiveConstructor Operator">,⋆</a> <a id="1463" href="Type.BetaNBE.html#1438" class="Bound">K</a><a id="1464" class="Symbol">)</a> <a id="1466" href="Type.BetaNBE.html#1434" class="Bound">σ</a>
<a id="1468" href="Type.BetaNBE.html#1419" class="Function">weakenVal</a> <a id="1478" class="Symbol">=</a> <a id="1480" href="Type.BetaNBE.html#1176" class="Function">renVal</a> <a id="1487" href="Type.html#1304" class="InductiveConstructor">S</a>
<a id="1489" class="Markup">\end{code}</a><a id="1499" class="Background">

Reify takes a value and yields a normal form.

</a><a id="1548" class="Markup">\begin{code}</a>
<a id="reify"></a><a id="1561" href="Type.BetaNBE.html#1561" class="Function">reify</a> <a id="1567" class="Symbol">:</a> <a id="1569" class="Symbol">∀</a> <a id="1571" class="Symbol">{</a><a id="1572" href="Type.BetaNBE.html#1572" class="Bound">σ</a> <a id="1574" href="Type.BetaNBE.html#1574" class="Bound">Φ</a><a id="1575" class="Symbol">}</a> <a id="1577" class="Symbol">→</a> <a id="1579" href="Type.BetaNBE.html#568" class="Function">Val</a> <a id="1583" href="Type.BetaNBE.html#1574" class="Bound">Φ</a> <a id="1585" href="Type.BetaNBE.html#1572" class="Bound">σ</a> <a id="1587" class="Symbol">→</a> <a id="1589" href="Type.BetaNBE.html#1574" class="Bound">Φ</a> <a id="1591" href="Type.BetaNormal.html#885" class="Datatype Operator">⊢Nf⋆</a> <a id="1596" href="Type.BetaNBE.html#1572" class="Bound">σ</a>
<a id="1598" href="Type.BetaNBE.html#1561" class="Function">reify</a> <a id="1604" class="Symbol">{</a><a id="1605" href="Type.html#615" class="InductiveConstructor">*</a><a id="1606" class="Symbol">}</a>     <a id="1612" href="Type.BetaNBE.html#1612" class="Bound">n</a>         <a id="1622" class="Symbol">=</a> <a id="1624" href="Type.BetaNBE.html#1612" class="Bound">n</a>
<a id="1626" href="Type.BetaNBE.html#1561" class="Function">reify</a> <a id="1632" class="Symbol">{</a><a id="1633" href="Type.BetaNBE.html#1633" class="Bound">σ</a> <a id="1635" href="Type.html#650" class="InductiveConstructor Operator">⇒</a> <a id="1637" href="Type.BetaNBE.html#1637" class="Bound">τ</a><a id="1638" class="Symbol">}</a> <a id="1640" class="Symbol">(</a><a id="1641" href="Data.Sum.Base.html#565" class="InductiveConstructor">inj₁</a> <a id="1646" href="Type.BetaNBE.html#1646" class="Bound">n</a><a id="1647" class="Symbol">)</a>  <a id="1650" class="Symbol">=</a> <a id="1652" href="Type.BetaNormal.html#1449" class="InductiveConstructor">ne</a> <a id="1655" href="Type.BetaNBE.html#1646" class="Bound">n</a>
<a id="1657" href="Type.BetaNBE.html#1561" class="Function">reify</a> <a id="1663" class="Symbol">{</a><a id="1664" href="Type.BetaNBE.html#1664" class="Bound">σ</a> <a id="1666" href="Type.html#650" class="InductiveConstructor Operator">⇒</a> <a id="1668" href="Type.BetaNBE.html#1668" class="Bound">τ</a><a id="1669" class="Symbol">}</a> <a id="1671" class="Symbol">(</a><a id="1672" href="Data.Sum.Base.html#590" class="InductiveConstructor">inj₂</a> <a id="1677" class="Symbol">(</a><a id="1678" href="Type.BetaNBE.html#1678" class="Bound">x</a> <a id="1680" href="Agda.Builtin.Sigma.html#209" class="InductiveConstructor Operator">,</a> <a id="1682" href="Type.BetaNBE.html#1682" class="Bound">f</a><a id="1683" class="Symbol">))</a>  <a id="1687" class="Symbol">=</a> <a id="1689" href="Type.BetaNormal.html#1359" class="InductiveConstructor">ƛ</a> <a id="1691" href="Type.BetaNBE.html#1678" class="Bound">x</a> <a id="1693" class="Symbol">(</a><a id="1694" href="Type.BetaNBE.html#1561" class="Function">reify</a> <a id="1700" class="Symbol">(</a><a id="1701" href="Type.BetaNBE.html#1682" class="Bound">f</a> <a id="1703" href="Type.html#1304" class="InductiveConstructor">S</a> <a id="1705" href="Type.BetaNBE.html#1075" class="Function">fresh</a><a id="1710" class="Symbol">))</a> <a id="1713" class="Comment">-- has a name been lost here?</a>
<a id="1743" class="Markup">\end{code}</a><a id="1753" class="Background">

An environment is a mapping from variables to values

</a><a id="1809" class="Markup">\begin{code}</a>
<a id="Env"></a><a id="1822" href="Type.BetaNBE.html#1822" class="Function">Env</a> <a id="1826" class="Symbol">:</a> <a id="1828" href="Type.html#866" class="Datatype">Ctx⋆</a> <a id="1833" class="Symbol">→</a> <a id="1835" href="Type.html#866" class="Datatype">Ctx⋆</a> <a id="1840" class="Symbol">→</a> <a id="1842" class="PrimitiveType">Set</a>
<a id="1846" href="Type.BetaNBE.html#1822" class="Function">Env</a> <a id="1850" href="Type.BetaNBE.html#1850" class="Bound">Ψ</a> <a id="1852" href="Type.BetaNBE.html#1852" class="Bound">Φ</a> <a id="1854" class="Symbol">=</a> <a id="1856" class="Symbol">∀{</a><a id="1858" href="Type.BetaNBE.html#1858" class="Bound">J</a><a id="1859" class="Symbol">}</a> <a id="1861" class="Symbol">→</a> <a id="1863" href="Type.BetaNBE.html#1850" class="Bound">Ψ</a> <a id="1865" href="Type.html#1217" class="Datatype Operator">∋⋆</a> <a id="1868" href="Type.BetaNBE.html#1858" class="Bound">J</a> <a id="1870" class="Symbol">→</a> <a id="1872" href="Type.BetaNBE.html#568" class="Function">Val</a> <a id="1876" href="Type.BetaNBE.html#1852" class="Bound">Φ</a> <a id="1878" href="Type.BetaNBE.html#1858" class="Bound">J</a>
<a id="1880" class="Markup">\end{code}</a><a id="1890" class="Background">

&#39;cons&#39; for environments

</a><a id="1917" class="Markup">\begin{code}</a>
<a id="_,,⋆_"></a><a id="1930" href="Type.BetaNBE.html#1930" class="Function Operator">_,,⋆_</a> <a id="1936" class="Symbol">:</a> <a id="1938" class="Symbol">∀{</a><a id="1940" href="Type.BetaNBE.html#1940" class="Bound">Ψ</a> <a id="1942" href="Type.BetaNBE.html#1942" class="Bound">Φ</a><a id="1943" class="Symbol">}</a> <a id="1945" class="Symbol">→</a> <a id="1947" class="Symbol">(</a><a id="1948" href="Type.BetaNBE.html#1948" class="Bound">σ</a> <a id="1950" class="Symbol">:</a> <a id="1952" href="Type.BetaNBE.html#1822" class="Function">Env</a> <a id="1956" href="Type.BetaNBE.html#1942" class="Bound">Φ</a> <a id="1958" href="Type.BetaNBE.html#1940" class="Bound">Ψ</a><a id="1959" class="Symbol">)</a> <a id="1961" class="Symbol">→</a> <a id="1963" class="Symbol">∀{</a><a id="1965" href="Type.BetaNBE.html#1965" class="Bound">K</a><a id="1966" class="Symbol">}(</a><a id="1968" href="Type.BetaNBE.html#1968" class="Bound">A</a> <a id="1970" class="Symbol">:</a> <a id="1972" href="Type.BetaNBE.html#568" class="Function">Val</a> <a id="1976" href="Type.BetaNBE.html#1940" class="Bound">Ψ</a> <a id="1978" href="Type.BetaNBE.html#1965" class="Bound">K</a><a id="1979" class="Symbol">)</a> <a id="1981" class="Symbol">→</a> <a id="1983" href="Type.BetaNBE.html#1822" class="Function">Env</a> <a id="1987" class="Symbol">(</a><a id="1988" href="Type.BetaNBE.html#1942" class="Bound">Φ</a> <a id="1990" href="Type.html#896" class="InductiveConstructor Operator">,⋆</a> <a id="1993" href="Type.BetaNBE.html#1965" class="Bound">K</a><a id="1994" class="Symbol">)</a> <a id="1996" href="Type.BetaNBE.html#1940" class="Bound">Ψ</a>
<a id="1998" class="Symbol">(</a><a id="1999" href="Type.BetaNBE.html#1999" class="Bound">σ</a> <a id="2001" href="Type.BetaNBE.html#1930" class="Function Operator">,,⋆</a> <a id="2005" href="Type.BetaNBE.html#2005" class="Bound">A</a><a id="2006" class="Symbol">)</a> <a id="2008" href="Type.html#1251" class="InductiveConstructor">Z</a>     <a id="2014" class="Symbol">=</a> <a id="2016" href="Type.BetaNBE.html#2005" class="Bound">A</a>
<a id="2018" class="Symbol">(</a><a id="2019" href="Type.BetaNBE.html#2019" class="Bound">σ</a> <a id="2021" href="Type.BetaNBE.html#1930" class="Function Operator">,,⋆</a> <a id="2025" href="Type.BetaNBE.html#2025" class="Bound">A</a><a id="2026" class="Symbol">)</a> <a id="2028" class="Symbol">(</a><a id="2029" href="Type.html#1304" class="InductiveConstructor">S</a> <a id="2031" href="Type.BetaNBE.html#2031" class="Bound">α</a><a id="2032" class="Symbol">)</a> <a id="2034" class="Symbol">=</a> <a id="2036" href="Type.BetaNBE.html#2019" class="Bound">σ</a> <a id="2038" href="Type.BetaNBE.html#2031" class="Bound">α</a>
<a id="2040" class="Markup">\end{code}</a><a id="2050" class="Background">

</a><a id="2052" class="Markup">\begin{code}</a>
<a id="exte"></a><a id="2065" href="Type.BetaNBE.html#2065" class="Function">exte</a> <a id="2070" class="Symbol">:</a> <a id="2072" class="Symbol">∀</a> <a id="2074" class="Symbol">{</a><a id="2075" href="Type.BetaNBE.html#2075" class="Bound">Φ</a> <a id="2077" href="Type.BetaNBE.html#2077" class="Bound">Ψ</a><a id="2078" class="Symbol">}</a> <a id="2080" class="Symbol">→</a> <a id="2082" href="Type.BetaNBE.html#1822" class="Function">Env</a> <a id="2086" href="Type.BetaNBE.html#2075" class="Bound">Φ</a> <a id="2088" href="Type.BetaNBE.html#2077" class="Bound">Ψ</a> <a id="2090" class="Symbol">→</a> <a id="2092" class="Symbol">(∀</a> <a id="2095" class="Symbol">{</a><a id="2096" href="Type.BetaNBE.html#2096" class="Bound">K</a><a id="2097" class="Symbol">}</a> <a id="2099" class="Symbol">→</a> <a id="2101" href="Type.BetaNBE.html#1822" class="Function">Env</a> <a id="2105" class="Symbol">(</a><a id="2106" href="Type.BetaNBE.html#2075" class="Bound">Φ</a> <a id="2108" href="Type.html#896" class="InductiveConstructor Operator">,⋆</a> <a id="2111" href="Type.BetaNBE.html#2096" class="Bound">K</a><a id="2112" class="Symbol">)</a> <a id="2114" class="Symbol">(</a><a id="2115" href="Type.BetaNBE.html#2077" class="Bound">Ψ</a> <a id="2117" href="Type.html#896" class="InductiveConstructor Operator">,⋆</a> <a id="2120" href="Type.BetaNBE.html#2096" class="Bound">K</a><a id="2121" class="Symbol">))</a>
<a id="2124" href="Type.BetaNBE.html#2065" class="Function">exte</a> <a id="2129" href="Type.BetaNBE.html#2129" class="Bound">η</a> <a id="2131" class="Symbol">=</a> <a id="2133" class="Symbol">(</a><a id="2134" href="Type.BetaNBE.html#1419" class="Function">weakenVal</a> <a id="2144" href="Function.html#805" class="Function Operator">∘</a> <a id="2146" href="Type.BetaNBE.html#2129" class="Bound">η</a><a id="2147" class="Symbol">)</a> <a id="2149" href="Type.BetaNBE.html#1930" class="Function Operator">,,⋆</a> <a id="2153" href="Type.BetaNBE.html#1075" class="Function">fresh</a>
<a id="2159" class="Comment">{-
-- this version would be more analogous to ext and exts but would
-- require changing some proofs for terms
exte η Z      = fresh
exte η (S α)  = weakenVal (η α)
-}</a>
<a id="2327" class="Markup">\end{code}</a><a id="2337" class="Background">


Application for values. As values at function type can be semantic
functions or neutral terms we need this function to unpack them. If
the function is neutral we create a neutral application by reifying
the value and applying the neutral application constructor, then we
refect the neutral application into a value. If the function is a
semantic function we apply it to the identity renaming and then to
the argument. In this case, the function and argument are in the same
context so we do not need the Kripke extension, hence the identity
renaming.

</a><a id="2892" class="Markup">\begin{code}</a>
<a id="_·V_"></a><a id="2905" href="Type.BetaNBE.html#2905" class="Function Operator">_·V_</a> <a id="2910" class="Symbol">:</a> <a id="2912" class="Symbol">∀{</a><a id="2914" href="Type.BetaNBE.html#2914" class="Bound">Φ</a> <a id="2916" href="Type.BetaNBE.html#2916" class="Bound">K</a> <a id="2918" href="Type.BetaNBE.html#2918" class="Bound">J</a><a id="2919" class="Symbol">}</a> <a id="2921" class="Symbol">→</a> <a id="2923" href="Type.BetaNBE.html#568" class="Function">Val</a> <a id="2927" href="Type.BetaNBE.html#2914" class="Bound">Φ</a> <a id="2929" class="Symbol">(</a><a id="2930" href="Type.BetaNBE.html#2916" class="Bound">K</a> <a id="2932" href="Type.html#650" class="InductiveConstructor Operator">⇒</a> <a id="2934" href="Type.BetaNBE.html#2918" class="Bound">J</a><a id="2935" class="Symbol">)</a> <a id="2937" class="Symbol">→</a> <a id="2939" href="Type.BetaNBE.html#568" class="Function">Val</a> <a id="2943" href="Type.BetaNBE.html#2914" class="Bound">Φ</a> <a id="2945" href="Type.BetaNBE.html#2916" class="Bound">K</a> <a id="2947" class="Symbol">→</a> <a id="2949" href="Type.BetaNBE.html#568" class="Function">Val</a> <a id="2953" href="Type.BetaNBE.html#2914" class="Bound">Φ</a> <a id="2955" href="Type.BetaNBE.html#2918" class="Bound">J</a>
<a id="2957" href="Data.Sum.Base.html#565" class="InductiveConstructor">inj₁</a> <a id="2962" href="Type.BetaNBE.html#2962" class="Bound">n</a> <a id="2964" href="Type.BetaNBE.html#2905" class="Function Operator">·V</a> <a id="2967" href="Type.BetaNBE.html#2967" class="Bound">v</a> <a id="2969" class="Symbol">=</a> <a id="2971" href="Type.BetaNBE.html#871" class="Function">reflect</a> <a id="2979" class="Symbol">(</a><a id="2980" href="Type.BetaNBE.html#2962" class="Bound">n</a> <a id="2982" href="Type.BetaNormal.html#1011" class="InductiveConstructor Operator">·</a> <a id="2984" href="Type.BetaNBE.html#1561" class="Function">reify</a> <a id="2990" href="Type.BetaNBE.html#2967" class="Bound">v</a><a id="2991" class="Symbol">)</a>
<a id="2993" href="Data.Sum.Base.html#590" class="InductiveConstructor">inj₂</a> <a id="2998" class="Symbol">(_</a> <a id="3001" href="Agda.Builtin.Sigma.html#209" class="InductiveConstructor Operator">,</a> <a id="3003" href="Type.BetaNBE.html#3003" class="Bound">f</a><a id="3004" class="Symbol">)</a> <a id="3006" href="Type.BetaNBE.html#2905" class="Function Operator">·V</a> <a id="3009" href="Type.BetaNBE.html#3009" class="Bound">v</a> <a id="3011" class="Symbol">=</a> <a id="3013" href="Type.BetaNBE.html#3003" class="Bound">f</a> <a id="3015" href="Function.html#1104" class="Function">id</a> <a id="3018" href="Type.BetaNBE.html#3009" class="Bound">v</a>
<a id="3020" class="Markup">\end{code}</a><a id="3030" class="Background">

Evaluation a term in an environment yields a value. The most
interesting cases are ƛ where we introduce a new Kripke function that
will evaluate when it receives an argument and Π/μ where we need to go
under the binder and extend the environement before evaluating and
reifying.

</a><a id="3312" class="Markup">\begin{code}</a>
<a id="eval"></a><a id="3325" href="Type.BetaNBE.html#3325" class="Function">eval</a> <a id="3330" class="Symbol">:</a> <a id="3332" class="Symbol">∀{</a><a id="3334" href="Type.BetaNBE.html#3334" class="Bound">Φ</a> <a id="3336" href="Type.BetaNBE.html#3336" class="Bound">Ψ</a> <a id="3338" href="Type.BetaNBE.html#3338" class="Bound">K</a><a id="3339" class="Symbol">}</a> <a id="3341" class="Symbol">→</a> <a id="3343" href="Type.BetaNBE.html#3336" class="Bound">Ψ</a> <a id="3345" href="Type.html#1945" class="Datatype Operator">⊢⋆</a> <a id="3348" href="Type.BetaNBE.html#3338" class="Bound">K</a> <a id="3350" class="Symbol">→</a> <a id="3352" href="Type.BetaNBE.html#1822" class="Function">Env</a> <a id="3356" href="Type.BetaNBE.html#3336" class="Bound">Ψ</a> <a id="3358" href="Type.BetaNBE.html#3334" class="Bound">Φ</a> <a id="3360" class="Symbol">→</a> <a id="3362" href="Type.BetaNBE.html#568" class="Function">Val</a> <a id="3366" href="Type.BetaNBE.html#3334" class="Bound">Φ</a> <a id="3368" href="Type.BetaNBE.html#3338" class="Bound">K</a>
<a id="3370" href="Type.BetaNBE.html#3325" class="Function">eval</a> <a id="3375" class="Symbol">(</a><a id="3376" href="Type.html#1979" class="InductiveConstructor">`</a> <a id="3378" href="Type.BetaNBE.html#3378" class="Bound">α</a><a id="3379" class="Symbol">)</a>       <a id="3387" href="Type.BetaNBE.html#3387" class="Bound">η</a> <a id="3389" class="Symbol">=</a> <a id="3391" href="Type.BetaNBE.html#3387" class="Bound">η</a> <a id="3393" href="Type.BetaNBE.html#3378" class="Bound">α</a>
<a id="3395" href="Type.BetaNBE.html#3325" class="Function">eval</a> <a id="3400" class="Symbol">(</a><a id="3401" href="Type.html#2035" class="InductiveConstructor">Π</a> <a id="3403" href="Type.BetaNBE.html#3403" class="Bound">x</a> <a id="3405" href="Type.BetaNBE.html#3405" class="Bound">B</a><a id="3406" class="Symbol">)</a>     <a id="3412" href="Type.BetaNBE.html#3412" class="Bound">η</a> <a id="3414" class="Symbol">=</a> <a id="3416" href="Type.BetaNormal.html#1205" class="InductiveConstructor">Π</a> <a id="3418" href="Type.BetaNBE.html#3403" class="Bound">x</a> <a id="3420" class="Symbol">(</a><a id="3421" href="Type.BetaNBE.html#1561" class="Function">reify</a> <a id="3427" class="Symbol">(</a><a id="3428" href="Type.BetaNBE.html#3325" class="Function">eval</a> <a id="3433" href="Type.BetaNBE.html#3405" class="Bound">B</a> <a id="3435" class="Symbol">(</a><a id="3436" href="Type.BetaNBE.html#2065" class="Function">exte</a> <a id="3441" href="Type.BetaNBE.html#3412" class="Bound">η</a><a id="3442" class="Symbol">)))</a>
<a id="3446" href="Type.BetaNBE.html#3325" class="Function">eval</a> <a id="3451" class="Symbol">(</a><a id="3452" href="Type.BetaNBE.html#3452" class="Bound">A</a> <a id="3454" href="Type.html#2112" class="InductiveConstructor Operator">⇒</a> <a id="3456" href="Type.BetaNBE.html#3456" class="Bound">B</a><a id="3457" class="Symbol">)</a>     <a id="3463" href="Type.BetaNBE.html#3463" class="Bound">η</a> <a id="3465" class="Symbol">=</a> <a id="3467" href="Type.BetaNBE.html#1561" class="Function">reify</a> <a id="3473" class="Symbol">(</a><a id="3474" href="Type.BetaNBE.html#3325" class="Function">eval</a> <a id="3479" href="Type.BetaNBE.html#3452" class="Bound">A</a> <a id="3481" href="Type.BetaNBE.html#3463" class="Bound">η</a><a id="3482" class="Symbol">)</a> <a id="3484" href="Type.BetaNormal.html#1286" class="InductiveConstructor Operator">⇒</a> <a id="3486" href="Type.BetaNBE.html#1561" class="Function">reify</a> <a id="3492" class="Symbol">(</a><a id="3493" href="Type.BetaNBE.html#3325" class="Function">eval</a> <a id="3498" href="Type.BetaNBE.html#3456" class="Bound">B</a> <a id="3500" href="Type.BetaNBE.html#3463" class="Bound">η</a><a id="3501" class="Symbol">)</a>
<a id="3503" href="Type.BetaNBE.html#3325" class="Function">eval</a> <a id="3508" class="Symbol">(</a><a id="3509" href="Type.html#2179" class="InductiveConstructor">ƛ</a> <a id="3511" href="Type.BetaNBE.html#3511" class="Bound">x</a> <a id="3513" href="Type.BetaNBE.html#3513" class="Bound">B</a><a id="3514" class="Symbol">)</a>     <a id="3520" href="Type.BetaNBE.html#3520" class="Bound">η</a> <a id="3522" class="Symbol">=</a> <a id="3524" href="Data.Sum.Base.html#590" class="InductiveConstructor">inj₂</a> <a id="3529" class="Symbol">(</a><a id="3530" href="Type.BetaNBE.html#3511" class="Bound">x</a> <a id="3532" href="Agda.Builtin.Sigma.html#209" class="InductiveConstructor Operator">,</a> <a id="3534" class="Symbol">λ</a> <a id="3536" href="Type.BetaNBE.html#3536" class="Bound">ρ</a> <a id="3538" href="Type.BetaNBE.html#3538" class="Bound">v</a> <a id="3540" class="Symbol">→</a> <a id="3542" href="Type.BetaNBE.html#3325" class="Function">eval</a> <a id="3547" href="Type.BetaNBE.html#3513" class="Bound">B</a> <a id="3549" class="Symbol">((</a><a id="3551" href="Type.BetaNBE.html#1176" class="Function">renVal</a> <a id="3558" href="Type.BetaNBE.html#3536" class="Bound">ρ</a> <a id="3560" href="Function.html#805" class="Function Operator">∘</a> <a id="3562" href="Type.BetaNBE.html#3520" class="Bound">η</a><a id="3563" class="Symbol">)</a> <a id="3565" href="Type.BetaNBE.html#1930" class="Function Operator">,,⋆</a> <a id="3569" href="Type.BetaNBE.html#3538" class="Bound">v</a><a id="3570" class="Symbol">))</a>
<a id="3573" href="Type.BetaNBE.html#3325" class="Function">eval</a> <a id="3578" class="Symbol">(</a><a id="3579" href="Type.BetaNBE.html#3579" class="Bound">A</a> <a id="3581" href="Type.html#2264" class="InductiveConstructor Operator">·</a> <a id="3583" href="Type.BetaNBE.html#3583" class="Bound">B</a><a id="3584" class="Symbol">)</a>     <a id="3590" href="Type.BetaNBE.html#3590" class="Bound">η</a> <a id="3592" class="Symbol">=</a> <a id="3594" href="Type.BetaNBE.html#3325" class="Function">eval</a> <a id="3599" href="Type.BetaNBE.html#3579" class="Bound">A</a> <a id="3601" href="Type.BetaNBE.html#3590" class="Bound">η</a> <a id="3603" href="Type.BetaNBE.html#2905" class="Function Operator">·V</a> <a id="3606" href="Type.BetaNBE.html#3325" class="Function">eval</a> <a id="3611" href="Type.BetaNBE.html#3583" class="Bound">B</a> <a id="3613" href="Type.BetaNBE.html#3590" class="Bound">η</a>
<a id="3615" href="Type.BetaNBE.html#3325" class="Function">eval</a> <a id="3620" href="Type.html#2338" class="InductiveConstructor">μ1</a>          <a id="3632" href="Type.BetaNBE.html#3632" class="Bound">η</a> <a id="3634" class="Symbol">=</a> <a id="3636" href="Data.Sum.Base.html#565" class="InductiveConstructor">inj₁</a> <a id="3641" href="Type.BetaNormal.html#1093" class="InductiveConstructor">μ1</a>
<a id="3644" href="Type.BetaNBE.html#3325" class="Function">eval</a> <a id="3649" class="Symbol">(</a><a id="3650" href="Type.html#2429" class="InductiveConstructor">con</a> <a id="3654" href="Type.BetaNBE.html#3654" class="Bound">tcn</a><a id="3657" class="Symbol">)</a>   <a id="3661" href="Type.BetaNBE.html#3661" class="Bound">η</a> <a id="3663" class="Symbol">=</a> <a id="3665" href="Type.BetaNormal.html#1509" class="InductiveConstructor">con</a> <a id="3669" href="Type.BetaNBE.html#3654" class="Bound">tcn</a>
<a id="3673" class="Markup">\end{code}</a><a id="3683" class="Background">

Identity environment

</a><a id="3707" class="Markup">\begin{code}</a>
<a id="idEnv"></a><a id="3720" href="Type.BetaNBE.html#3720" class="Function">idEnv</a> <a id="3726" class="Symbol">:</a> <a id="3728" class="Symbol">∀</a> <a id="3730" href="Type.BetaNBE.html#3730" class="Bound">Φ</a> <a id="3732" class="Symbol">→</a> <a id="3734" href="Type.BetaNBE.html#1822" class="Function">Env</a> <a id="3738" href="Type.BetaNBE.html#3730" class="Bound">Φ</a> <a id="3740" href="Type.BetaNBE.html#3730" class="Bound">Φ</a>
<a id="3742" href="Type.BetaNBE.html#3720" class="Function">idEnv</a> <a id="3748" href="Type.BetaNBE.html#3748" class="Bound">Φ</a> <a id="3750" class="Symbol">=</a> <a id="3752" href="Type.BetaNBE.html#871" class="Function">reflect</a> <a id="3760" href="Function.html#805" class="Function Operator">∘</a> <a id="3762" href="Type.BetaNormal.html#953" class="InductiveConstructor">`</a>
<a id="3764" class="Markup">\end{code}</a><a id="3774" class="Background">

Normalisating a term yields a normal form. We evaluate in the identity
environment to yield a value in the same context as the original term
and then reify to yield a normal form

</a><a id="3956" class="Markup">\begin{code}</a>
<a id="nf"></a><a id="3969" href="Type.BetaNBE.html#3969" class="Function">nf</a> <a id="3972" class="Symbol">:</a> <a id="3974" class="Symbol">∀{</a><a id="3976" href="Type.BetaNBE.html#3976" class="Bound">Φ</a> <a id="3978" href="Type.BetaNBE.html#3978" class="Bound">K</a><a id="3979" class="Symbol">}</a> <a id="3981" class="Symbol">→</a> <a id="3983" href="Type.BetaNBE.html#3976" class="Bound">Φ</a> <a id="3985" href="Type.html#1945" class="Datatype Operator">⊢⋆</a> <a id="3988" href="Type.BetaNBE.html#3978" class="Bound">K</a> <a id="3990" class="Symbol">→</a> <a id="3992" href="Type.BetaNBE.html#3976" class="Bound">Φ</a> <a id="3994" href="Type.BetaNormal.html#885" class="Datatype Operator">⊢Nf⋆</a> <a id="3999" href="Type.BetaNBE.html#3978" class="Bound">K</a>
<a id="4001" href="Type.BetaNBE.html#3969" class="Function">nf</a> <a id="4004" href="Type.BetaNBE.html#4004" class="Bound">t</a> <a id="4006" class="Symbol">=</a> <a id="4008" href="Type.BetaNBE.html#1561" class="Function">reify</a> <a id="4014" class="Symbol">(</a><a id="4015" href="Type.BetaNBE.html#3325" class="Function">eval</a> <a id="4020" href="Type.BetaNBE.html#4004" class="Bound">t</a> <a id="4022" class="Symbol">(</a><a id="4023" href="Type.BetaNBE.html#3720" class="Function">idEnv</a> <a id="4029" class="Symbol">_))</a>
<a id="4033" class="Markup">\end{code}</a><a id="4043" class="Background">
</a></pre></body></html>
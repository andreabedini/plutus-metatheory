<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Type.BetaNBE.Stability</title><link rel="stylesheet" href="Agda.css"></head><body><pre>\begin{code}
<a id="14" class="Keyword">module</a> <a id="21" href="Type.BetaNBE.Stability.html" class="Module">Type.BetaNBE.Stability</a> <a id="44" class="Keyword">where</a>
\end{code}<a id="60" class="Comment">

</a>\begin{code}
<a id="75" class="Keyword">open</a> <a id="80" class="Keyword">import</a> <a id="87" href="Type.html" class="Module">Type</a>
<a id="92" class="Keyword">open</a> <a id="97" class="Keyword">import</a> <a id="104" href="Type.BetaNormal.html" class="Module">Type.BetaNormal</a>
<a id="120" class="Keyword">open</a> <a id="125" class="Keyword">import</a> <a id="132" href="Type.BetaNBE.html" class="Module">Type.BetaNBE</a>
<a id="145" class="Keyword">open</a> <a id="150" class="Keyword">import</a> <a id="157" href="Type.BetaNBE.Completeness.html" class="Module">Type.BetaNBE.Completeness</a>

<a id="184" class="Keyword">open</a> <a id="189" class="Keyword">import</a> <a id="196" href="Relation.Binary.PropositionalEquality.html" class="Module">Relation.Binary.PropositionalEquality</a>
<a id="234" class="Keyword">open</a> <a id="239" class="Keyword">import</a> <a id="246" href="Function.html" class="Module">Function</a>
\end{code}<a id="265" class="Comment">

If you take a normal form, embed it back into syntax and then
normalize it again, you get the same result. This is an important
property for substitution on normal forms: we don&#39;t want to eta expand
variables otherwise substituting in by the identity substitution can
perturb the expression.

</a>\begin{code}
<a id="stability"></a><a id="573" href="Type.BetaNBE.Stability.html#573" class="Function">stability</a> <a id="583" class="Symbol">:</a> <a id="585" class="Symbol">∀{</a><a id="587" href="Type.BetaNBE.Stability.html#587" class="Bound">Φ</a> <a id="589" href="Type.BetaNBE.Stability.html#589" class="Bound">K</a><a id="590" class="Symbol">}(</a><a id="592" href="Type.BetaNBE.Stability.html#592" class="Bound">n</a> <a id="594" class="Symbol">:</a> <a id="596" href="Type.BetaNBE.Stability.html#587" class="Bound">Φ</a> <a id="598" href="Type.BetaNormal.html#862" class="Datatype Operator">⊢Nf⋆</a> <a id="603" href="Type.BetaNBE.Stability.html#589" class="Bound">K</a><a id="604" class="Symbol">)</a> <a id="606" class="Symbol">→</a> <a id="608" href="Type.BetaNBE.html#4075" class="Function">nf</a> <a id="611" class="Symbol">(</a><a id="612" href="Type.BetaNormal.html#5028" class="Function">embNf</a> <a id="618" href="Type.BetaNBE.Stability.html#592" class="Bound">n</a><a id="619" class="Symbol">)</a> <a id="621" href="Agda.Builtin.Equality.html#83" class="Datatype Operator">≡</a> <a id="623" href="Type.BetaNBE.Stability.html#592" class="Bound">n</a>
<a id="stabilityNeN"></a><a id="625" href="Type.BetaNBE.Stability.html#625" class="Function">stabilityNeN</a> <a id="638" class="Symbol">:</a> <a id="640" class="Symbol">∀{</a><a id="642" href="Type.BetaNBE.Stability.html#642" class="Bound">Φ</a> <a id="644" href="Type.BetaNBE.Stability.html#644" class="Bound">K</a><a id="645" class="Symbol">}(</a><a id="647" href="Type.BetaNBE.Stability.html#647" class="Bound">n</a> <a id="649" class="Symbol">:</a> <a id="651" href="Type.BetaNBE.Stability.html#642" class="Bound">Φ</a> <a id="653" href="Type.BetaNormal.html#895" class="Datatype Operator">⊢NeN⋆</a> <a id="659" href="Type.BetaNBE.Stability.html#644" class="Bound">K</a><a id="660" class="Symbol">)</a> <a id="662" class="Symbol">→</a> <a id="664" href="Type.BetaNBE.html#3388" class="Function">eval</a> <a id="669" class="Symbol">(</a><a id="670" href="Type.BetaNormal.html#5063" class="Function">embNeN</a> <a id="677" href="Type.BetaNBE.Stability.html#647" class="Bound">n</a><a id="678" class="Symbol">)</a> <a id="680" class="Symbol">(</a><a id="681" href="Type.BetaNBE.html#3826" class="Function">idEnv</a> <a id="687" class="Symbol">_)</a>  <a id="691" href="Agda.Builtin.Equality.html#83" class="Datatype Operator">≡</a> <a id="693" href="Type.BetaNBE.html#864" class="Function">reflect</a> <a id="701" href="Type.BetaNBE.Stability.html#647" class="Bound">n</a>

<a id="704" href="Type.BetaNBE.Stability.html#573" class="Function">stability</a> <a id="714" class="Symbol">(</a><a id="715" href="Type.BetaNormal.html#1187" class="InductiveConstructor">Π</a> <a id="717" href="Type.BetaNBE.Stability.html#717" class="Bound">B</a><a id="718" class="Symbol">)</a> <a id="720" class="Symbol">=</a>
  <a id="724" href="Relation.Binary.PropositionalEquality.html#1075" class="Function">cong</a> <a id="729" href="Type.BetaNormal.html#1187" class="InductiveConstructor">Π</a> <a id="731" class="Symbol">(</a><a id="732" href="Relation.Binary.PropositionalEquality.Core.html#627" class="Function">trans</a> <a id="738" class="Symbol">(</a><a id="739" href="Type.BetaNBE.Completeness.html#9431" class="Function">idext</a> <a id="745" class="Symbol">(λ</a> <a id="748" class="Symbol">{</a> <a id="750" href="Type.html#1286" class="InductiveConstructor">Z</a> <a id="752" class="Symbol">→</a> <a id="754" href="Type.BetaNBE.Completeness.html#3434" class="Function">reflectCR</a> <a id="764" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">refl</a>
                          <a id="795" class="Symbol">;</a> <a id="797" class="Symbol">(</a><a id="798" href="Type.html#1339" class="InductiveConstructor">S</a> <a id="800" href="Type.BetaNBE.Stability.html#800" class="Bound">α</a><a id="801" class="Symbol">)</a> <a id="803" class="Symbol">→</a> <a id="805" href="Type.BetaNBE.Completeness.html#5067" class="Function">renameVal-reflect</a> <a id="823" href="Type.html#1339" class="InductiveConstructor">S</a> <a id="825" class="Symbol">(</a><a id="826" href="Type.BetaNormal.html#931" class="InductiveConstructor">`</a> <a id="828" href="Type.BetaNBE.Stability.html#800" class="Bound">α</a><a id="829" class="Symbol">)})</a>
                       <a id="856" class="Symbol">(</a><a id="857" href="Type.BetaNormal.html#5028" class="Function">embNf</a> <a id="863" href="Type.BetaNBE.Stability.html#717" class="Bound">B</a><a id="864" class="Symbol">))</a>
                <a id="883" class="Symbol">(</a><a id="884" href="Type.BetaNBE.Stability.html#573" class="Function">stability</a> <a id="894" href="Type.BetaNBE.Stability.html#717" class="Bound">B</a><a id="895" class="Symbol">))</a>
<a id="898" href="Type.BetaNBE.Stability.html#573" class="Function">stability</a> <a id="908" class="Symbol">(</a><a id="909" href="Type.BetaNBE.Stability.html#909" class="Bound">A</a> <a id="911" href="Type.BetaNormal.html#1255" class="InductiveConstructor Operator">⇒</a> <a id="913" href="Type.BetaNBE.Stability.html#913" class="Bound">B</a><a id="914" class="Symbol">)</a> <a id="916" class="Symbol">=</a> <a id="918" href="Relation.Binary.PropositionalEquality.html#1313" class="Function">cong₂</a> <a id="924" href="Type.BetaNormal.html#1255" class="InductiveConstructor Operator">_⇒_</a> <a id="928" class="Symbol">(</a><a id="929" href="Type.BetaNBE.Stability.html#573" class="Function">stability</a> <a id="939" href="Type.BetaNBE.Stability.html#909" class="Bound">A</a><a id="940" class="Symbol">)</a> <a id="942" class="Symbol">(</a><a id="943" href="Type.BetaNBE.Stability.html#573" class="Function">stability</a> <a id="953" href="Type.BetaNBE.Stability.html#913" class="Bound">B</a><a id="954" class="Symbol">)</a>
<a id="956" href="Type.BetaNBE.Stability.html#573" class="Function">stability</a> <a id="966" class="Symbol">(</a><a id="967" href="Type.BetaNormal.html#1328" class="InductiveConstructor">ƛ</a> <a id="969" href="Type.BetaNBE.Stability.html#969" class="Bound">B</a><a id="970" class="Symbol">)</a>    <a id="975" class="Symbol">=</a>
  <a id="979" href="Relation.Binary.PropositionalEquality.html#1075" class="Function">cong</a> <a id="984" href="Type.BetaNormal.html#1328" class="InductiveConstructor">ƛ</a> <a id="986" class="Symbol">(</a><a id="987" href="Relation.Binary.PropositionalEquality.Core.html#627" class="Function">trans</a> <a id="993" class="Symbol">(</a><a id="994" href="Type.BetaNBE.Completeness.html#3662" class="Function">reifyCR</a> <a id="1002" class="Symbol">(</a><a id="1003" href="Type.BetaNBE.Completeness.html#9431" class="Function">idext</a> <a id="1009" class="Symbol">(λ</a> <a id="1012" class="Symbol">{</a> <a id="1014" href="Type.html#1286" class="InductiveConstructor">Z</a> <a id="1016" class="Symbol">→</a> <a id="1018" href="Type.BetaNBE.Completeness.html#3434" class="Function">reflectCR</a> <a id="1028" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">refl</a>
                                      <a id="1071" class="Symbol">;</a> <a id="1073" class="Symbol">(</a><a id="1074" href="Type.html#1339" class="InductiveConstructor">S</a> <a id="1076" href="Type.BetaNBE.Stability.html#1076" class="Bound">α</a><a id="1077" class="Symbol">)</a> <a id="1079" class="Symbol">→</a> <a id="1081" href="Type.BetaNBE.Completeness.html#5067" class="Function">renameVal-reflect</a> <a id="1099" href="Type.html#1339" class="InductiveConstructor">S</a> <a id="1101" class="Symbol">(</a><a id="1102" href="Type.BetaNormal.html#931" class="InductiveConstructor">`</a> <a id="1104" href="Type.BetaNBE.Stability.html#1076" class="Bound">α</a><a id="1105" class="Symbol">)})</a>
                                   <a id="1144" class="Symbol">(</a><a id="1145" href="Type.BetaNormal.html#5028" class="Function">embNf</a> <a id="1151" href="Type.BetaNBE.Stability.html#969" class="Bound">B</a><a id="1152" class="Symbol">)))</a>
                <a id="1172" class="Symbol">(</a><a id="1173" href="Type.BetaNBE.Stability.html#573" class="Function">stability</a> <a id="1183" href="Type.BetaNBE.Stability.html#969" class="Bound">B</a><a id="1184" class="Symbol">))</a>
<a id="1187" href="Type.BetaNBE.Stability.html#573" class="Function">stability</a> <a id="1197" class="Symbol">(</a><a id="1198" href="Type.BetaNormal.html#1466" class="InductiveConstructor">size⋆</a> <a id="1204" href="Type.BetaNBE.Stability.html#1204" class="Bound">n</a><a id="1205" class="Symbol">)</a>   <a id="1209" class="Symbol">=</a> <a id="1211" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">refl</a>
<a id="1216" href="Type.BetaNBE.Stability.html#573" class="Function">stability</a> <a id="1226" class="Symbol">(</a><a id="1227" href="Type.BetaNormal.html#1499" class="InductiveConstructor">con</a> <a id="1231" href="Type.BetaNBE.Stability.html#1231" class="Bound">tcn</a> <a id="1235" href="Type.BetaNBE.Stability.html#1235" class="Bound">s</a><a id="1236" class="Symbol">)</a> <a id="1238" class="Symbol">=</a> <a id="1240" href="Relation.Binary.PropositionalEquality.html#1075" class="Function">cong</a> <a id="1245" class="Symbol">(</a><a id="1246" href="Type.BetaNormal.html#1499" class="InductiveConstructor">con</a> <a id="1250" href="Type.BetaNBE.Stability.html#1231" class="Bound">tcn</a><a id="1253" class="Symbol">)</a> <a id="1255" class="Symbol">(</a><a id="1256" href="Type.BetaNBE.Stability.html#573" class="Function">stability</a> <a id="1266" href="Type.BetaNBE.Stability.html#1235" class="Bound">s</a><a id="1267" class="Symbol">)</a>
<a id="1269" href="Type.BetaNBE.Stability.html#573" class="Function">stability</a> <a id="1279" class="Symbol">{</a><a id="1280" class="Argument">K</a> <a id="1282" class="Symbol">=</a> <a id="1284" href="Type.html#615" class="InductiveConstructor">*</a><a id="1285" class="Symbol">}</a>     <a id="1291" class="Symbol">(</a><a id="1292" href="Type.BetaNormal.html#1405" class="InductiveConstructor">ne</a> <a id="1295" href="Type.BetaNBE.Stability.html#1295" class="Bound">n</a><a id="1296" class="Symbol">)</a> <a id="1298" class="Symbol">=</a> <a id="1300" href="Type.BetaNBE.Stability.html#625" class="Function">stabilityNeN</a> <a id="1313" href="Type.BetaNBE.Stability.html#1295" class="Bound">n</a>
<a id="1315" href="Type.BetaNBE.Stability.html#573" class="Function">stability</a> <a id="1325" class="Symbol">{</a><a id="1326" class="Argument">K</a> <a id="1328" class="Symbol">=</a> <a id="1330" href="Type.html#650" class="InductiveConstructor">#</a><a id="1331" class="Symbol">}</a>     <a id="1337" class="Symbol">(</a><a id="1338" href="Type.BetaNormal.html#1405" class="InductiveConstructor">ne</a> <a id="1341" href="Type.BetaNBE.Stability.html#1341" class="Bound">n</a><a id="1342" class="Symbol">)</a> <a id="1344" class="Symbol">=</a> <a id="1346" href="Type.BetaNBE.Stability.html#625" class="Function">stabilityNeN</a> <a id="1359" href="Type.BetaNBE.Stability.html#1341" class="Bound">n</a>
<a id="1361" href="Type.BetaNBE.Stability.html#573" class="Function">stability</a> <a id="1371" class="Symbol">{</a><a id="1372" class="Argument">K</a> <a id="1374" class="Symbol">=</a> <a id="1376" href="Type.BetaNBE.Stability.html#1376" class="Bound">K</a> <a id="1378" href="Type.html#685" class="InductiveConstructor Operator">⇒</a> <a id="1380" href="Type.BetaNBE.Stability.html#1380" class="Bound">J</a><a id="1381" class="Symbol">}</a> <a id="1383" class="Symbol">(</a><a id="1384" href="Type.BetaNormal.html#1405" class="InductiveConstructor">ne</a> <a id="1387" href="Type.BetaNBE.Stability.html#1387" class="Bound">n</a><a id="1388" class="Symbol">)</a> <a id="1390" class="Symbol">=</a> <a id="1392" href="Relation.Binary.PropositionalEquality.html#1075" class="Function">cong</a> <a id="1397" class="Symbol">(λ</a> <a id="1400" href="Type.BetaNBE.Stability.html#1400" class="Bound">v</a> <a id="1402" class="Symbol">→</a> <a id="1404" href="Type.BetaNBE.html#1640" class="Function">reify</a> <a id="1410" href="Type.BetaNBE.Stability.html#1400" class="Bound">v</a><a id="1411" class="Symbol">)</a> <a id="1413" class="Symbol">(</a><a id="1414" href="Type.BetaNBE.Stability.html#625" class="Function">stabilityNeN</a> <a id="1427" href="Type.BetaNBE.Stability.html#1387" class="Bound">n</a><a id="1428" class="Symbol">)</a>

<a id="1431" href="Type.BetaNBE.Stability.html#625" class="Function">stabilityNeN</a> <a id="1444" class="Symbol">(</a><a id="1445" href="Type.BetaNormal.html#931" class="InductiveConstructor">`</a> <a id="1447" href="Type.BetaNBE.Stability.html#1447" class="Bound">α</a><a id="1448" class="Symbol">)</a>    <a id="1453" class="Symbol">=</a> <a id="1455" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">refl</a>
<a id="1460" href="Type.BetaNBE.Stability.html#625" class="Function">stabilityNeN</a> <a id="1473" class="Symbol">(</a><a id="1474" href="Type.BetaNBE.Stability.html#1474" class="Bound">n</a> <a id="1476" href="Type.BetaNormal.html#990" class="InductiveConstructor Operator">·</a> <a id="1478" href="Type.BetaNBE.Stability.html#1478" class="Bound">n&#39;</a><a id="1480" class="Symbol">)</a> <a id="1482" class="Symbol">=</a>
  <a id="1486" href="Relation.Binary.PropositionalEquality.Core.html#627" class="Function">trans</a> <a id="1492" class="Symbol">(</a><a id="1493" href="Relation.Binary.PropositionalEquality.html#1075" class="Function">cong</a> <a id="1498" class="Symbol">(</a><a id="1499" href="Type.BetaNBE.html#2974" class="Function Operator">_·V</a> <a id="1503" class="Symbol">(</a><a id="1504" href="Type.BetaNBE.html#3388" class="Function">eval</a> <a id="1509" class="Symbol">(</a><a id="1510" href="Type.BetaNormal.html#5028" class="Function">embNf</a> <a id="1516" href="Type.BetaNBE.Stability.html#1478" class="Bound">n&#39;</a><a id="1518" class="Symbol">)</a> <a id="1520" class="Symbol">(</a><a id="1521" href="Type.BetaNBE.html#3826" class="Function">idEnv</a> <a id="1527" class="Symbol">_)))</a> <a id="1532" class="Symbol">(</a><a id="1533" href="Type.BetaNBE.Stability.html#625" class="Function">stabilityNeN</a> <a id="1546" href="Type.BetaNBE.Stability.html#1474" class="Bound">n</a><a id="1547" class="Symbol">))</a>
        <a id="1558" class="Symbol">(</a><a id="1559" href="Relation.Binary.PropositionalEquality.html#1075" class="Function">cong</a> <a id="1564" class="Symbol">(λ</a> <a id="1567" href="Type.BetaNBE.Stability.html#1567" class="Bound">n&#39;&#39;</a> <a id="1571" class="Symbol">→</a> <a id="1573" href="Type.BetaNBE.html#864" class="Function">reflect</a> <a id="1581" class="Symbol">(</a><a id="1582" href="Type.BetaNBE.Stability.html#1474" class="Bound">n</a> <a id="1584" href="Type.BetaNormal.html#990" class="InductiveConstructor Operator">·</a> <a id="1586" href="Type.BetaNBE.Stability.html#1567" class="Bound">n&#39;&#39;</a><a id="1589" class="Symbol">))</a> <a id="1592" class="Symbol">(</a><a id="1593" href="Type.BetaNBE.Stability.html#573" class="Function">stability</a> <a id="1603" href="Type.BetaNBE.Stability.html#1478" class="Bound">n&#39;</a><a id="1605" class="Symbol">))</a>
<a id="1608" href="Type.BetaNBE.Stability.html#625" class="Function">stabilityNeN</a> <a id="1621" href="Type.BetaNormal.html#1074" class="InductiveConstructor">μ1</a>      <a id="1629" class="Symbol">=</a> <a id="1631" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">refl</a>
\end{code}<a id="1646" class="Comment">
</a></pre></body></html>